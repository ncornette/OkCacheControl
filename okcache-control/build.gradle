plugins {
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId "${POM_GROUPID}"
            artifactId "${POM_ARTIFACTID}"
            version "${POM_VERSION}"

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    dryRun = false
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = "${POM_ARTIFACTID}"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ncornette/OkCacheControl.git'
        version {
            name= "${POM_VERSION}"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
    testCompile 'org.assertj:assertj-core:2.2.0'
    testCompile 'org.mockito:mockito-core:2.0.54-beta'
}
